;;; jsc-utils.lisp --- Utilities on top of JSCore.

;; This file is part of cl-webkit.
;;
;; cl-webkit is free software; you can redistribute it and/or modify
;; it under the terms of the MIT license.
;; See `COPYING' in the source distribution for details.

;;; Code:

(in-package #:webkit2)

(defvar %context-lock (bt:make-lock))
(defvar %context nil)

(export 'get-jsc-context)
(declaim (ftype (function (webkit-web-view (or null string jsc-context))) get-jsc-context))
(defun get-jsc-context (view &optional designator)
  "Get a JSCContext for VIEW, matching DESIGNATOR.
DESIGNATOR could be:
- A `jsc-context' -- return it back.
- A string -- return a context of the world named by DESIGNATOR.
- A nil -- return the default context of VIEW."
  (bt:with-lock-held (%context-lock)
    (let ((context-callback
            (lambda (result jsc-value)
              (declare (ignore result))
              (setf %context (jsc-value-get-context jsc-value)))))
      (gtk:within-gtk-thread
        (typecase designator
          (jsc-context (setf %context designator))
          (string (webkit:webkit-web-view-evaluate-javascript
                   view "null" context-callback nil designator))
          (null (webkit:webkit-web-view-evaluate-javascript
                 view "null" context-callback))))
      (loop until %context
            finally (return (prog1 %context
                              (setf %context nil)))))))

(export 'jsc-value-to-lisp)
(declaim (ftype (function (t &key (:null-value t)
                             (:undefined-value t)
                             (:false-value t)
                             (:true-value t)
                             (:array-type (or null (member :list :vector)))
                             (:object-type (or null (member :alist :plist :hash-table)))))
                jsc-value-to-lisp))
(defun jsc-value-to-lisp (jsc-value &key (null-value *js-null-value*)
                                      (undefined-value *js-undefined-value*)
                                      (false-value *js-false-value*)
                                      (true-value *js-true-value*)
                                      (array-type *js-array-type*)
                                      (object-type *js-object-type*))
  "Translate a JSC-VALUE to a lisp value.
Translates:
- JS strings to strings.
- JS numbers:
  - to integers if the absolute value is less than 1.0d22,
  - floating-point numbers otherwise.
- true to TRUE-VALUE (t by default). Also see `*js-true-value*'.
- false to FALSE-VALUE (nil by default). Also see `*js-false-value*'.
- null to NULL-VALUE (:null by default). Also see `*js-null-value*'.
- undefined to UNDEFINED-VALUE (:undefined by default). Also see `*js-undefined-value*'.
- JS arrays to either lists or vectors (:list and :vector ARRAY-TYPE respectively).
  Also see `*js-array-type*'.
- JS objects (also see `*js-object-type*') to:
  - hash-tables (if OBJECT-TYPE is :hash-table, default) with string keys,
  - alists (if OBJECT-TYPE is :alist) with string keys,
  - plists (if OBJECT-TYPE is :plist) with keyword keys,"
  (flet ((drain-properties (jsc-value array-p)
           (loop with property-names = (jsc-value-object-enumerate-properties jsc-value)
                 for index from 0
                 until (cffi:null-pointer-p property-names)
                 for property-name = (cffi:mem-aref property-names '(:pointer (:pointer :char)) index)
                 until (cffi:null-pointer-p property-name)
                 collect (let ((property-value
                                 (jsc-value-to-lisp
                                  (if array-p
                                      (jsc-value-object-get-property-at-index jsc-value index)
                                      (jsc-value-object-get-property jsc-value property-name)))))
                           (if array-p
                               property-value
                               (list (cffi:foreign-string-to-lisp property-name) property-value)))
                   into properties
                 finally (return
                           (progn
                             (dotimes (i index)
                               (cffi:foreign-array-free
                                (cffi:mem-aref property-names '(:pointer (:pointer :char)) i)))
                             (cffi:foreign-array-free property-names)
                             properties)))))
    (cond
      ((jsc-value-is-null jsc-value) null-value)
      ((jsc-value-is-undefined jsc-value) undefined-value)
      ((jsc-value-is-number jsc-value) (let ((num (jsc-value-to-double jsc-value)))
                                         (handler-case
                                             ;; Floats get too chaotic after 1.0d22.
                                             ;; Looking at floating part doesn't help there.
                                             (if (and (< (abs num) 1.0d22) (zerop (rem num 1.0)))
                                                 (truncate num)
                                                 num)
                                           ;; Truncation/comparison can error out on infinity/NaN.
                                           (error (c) (declare (ignore c)) num))))
      ((jsc-value-is-string jsc-value) (jsc-value-to-string jsc-value))
      ((jsc-value-is-boolean jsc-value)
       (if (jsc-value-to-boolean jsc-value)
           true-value
           false-value))
      ((jsc-value-is-array jsc-value)
       (let ((elements (drain-properties jsc-value t)))
         (if (eq array-type :list)
             elements
             (coerce elements 'vector))))
      ((jsc-value-is-function jsc-value)
       (lambda (&rest args)
         (jsc-value-to-lisp
          (jsc-value-function-callv
           jsc-value
           (length args)
           (if args
               ;; TODO: Free this.
               (cffi:foreign-alloc :pointer
                                   :initial-contents
                                   (mapcar #'pointer
                                           (mapcar (lambda (arg)
                                                     (lisp-to-jsc-value
                                                      arg (jsc-value-get-context jsc-value)))
                                                   args))
                                   :count (length args))
               (null-pointer))))))
      ((jsc-value-is-object jsc-value)
       (let ((properties (drain-properties jsc-value nil)))
         (case object-type
           (:hash-table
            (loop with object = (make-hash-table :test 'equal)
                  for (name value) in properties
                  do (setf (gethash name object) value)
                  finally (return object)))
           (:alist properties)
           (:plist (apply #'append (mapcar
                                    (lambda (property)
                                      (list (intern (string-upcase (first property)) :keyword)
                                            (second property)))
                                    properties)))))))))

(export 'lisp-to-jsc-value)
(defgeneric lisp-to-jsc-value (value &optional context)
  (:method ((value t) &optional (context (jsc-context-get-current)))
    (declare (ignore value))
    (jsc-value-new-undefined context))
  (:documentation "Transform a provided Lisp value to the most sensible JSCValue counterpart.
In case no suitable method was found, create a JSCValue for undefined."))

(defmethod lisp-to-jsc-value ((number real) &optional (context (jsc-context-get-current)))
  (jsc-value-new-number context (coerce number 'double-float)))

(defmethod lisp-to-jsc-value ((string string) &optional (context (jsc-context-get-current)))
  (jsc-value-new-string context string))

(defmethod lisp-to-jsc-value ((symbol symbol) &optional (context (jsc-context-get-current)))
  (cond
    ((eq symbol *js-null-value*) (jsc-value-new-null context))
    ((eq symbol *js-undefined-value*) (jsc-value-new-undefined context))
    ((eq symbol *js-true-value*) (jsc-value-new-boolean context t))
    ((eq symbol *js-false-value*) (jsc-value-new-boolean context nil))
    (t (call-next-method (symbol-name symbol) context))))

(defmethod lisp-to-jsc-value ((list list) &optional (context (jsc-context-get-current)))
  (jsc-value-new-array-from-list
   context (mapcar (lambda (value) (lisp-to-jsc-value value context)) list)))

(defmethod lisp-to-jsc-value ((vector vector) &optional (context (jsc-context-get-current)))
  (jsc-value-new-array-from-list
   context (map 'list (lambda (value) (lisp-to-jsc-value value context)) vector)))

(defmethod lisp-to-jsc-value ((hash-table hash-table) &optional (context (jsc-context-get-current)))
  (let (json-alist)
    (maphash
     (lambda (key value)
       (push (list key (jsc-value-to-json (lisp-to-jsc-value value context) 0)) json-alist))
     hash-table)
    (jsc-value-new-from-json context (format nil "{蠛狺蝴蠛狺ㄦ轵篝牦镱犰轶舂蝈篝牦镱犰轶舂┅┅ㄤ邈灬轫ㄦ豉疱ㄦ躅泗轱麇怆轸麇猸鲩鬻矧铛祆篝蜷铉牦悱泔铘屮舂篝蜷铉轭翦珏颟ロ犭瀛牦悱骢钽糸镱┅ㄤ彐躅ロ犭瀛牦悱骢钽糸镱鲩鬻泔铘屮舡溴箝珙狒矧钺礤汜祆忉汶瞽狎珞戾è牦悱鲠祯瀛豉疱ㄦ矧彘珙骢钽犰㈥筱喏犰蹂哏弭唪疱吼镩铘弪┅牦悱鲠祯瀛麸扉箴牦悱鲠祯瀛铄鳝骢钽糸镱ㄧ弭牦悱泔铘屮鲩鬻泔铘屮舡溴箝珙狒矧钺礤汜祆忉汶ㄣ骀楹铛祆痫轭翦颟ㄣ骀楹铛祆痫轭翦颟换韵南阵绛铒糸纟溴篝蝻骝邋牦悱鲠祯瀛豉疱瞽狎珞ㄣ骀楹骘蝈殓瞽犰祜吼镩铘弪洪铋糸犰泔铘孱趔祜镳蝈疱狒瞽狎珞泔祆邈牦悱鲠祯瀛豉疱┅┅┅ㄥ痫螋ы犭瀛牦悱骢钽糸镱ㄤ彐磲泸磲脲牦悱骢钽糸镱礤翎溽翎狎珞怙澌怙澌⒚蝈狒铄视弥犰蹂骢钽糸镱鏖翳汜祆忉汶栳鲩铉烈怯犷孪馁团粤牧粤轶彘翳弪箝铉戾鲠祯稚抛翳狒翳骢钽糸镱鏖祆忮溴骈铄轭翳泔铘屮镦矧ㄖ膳ο性上瘟瘟团孟卧咆原呐由俏猎弦扉篝澡骢钽糸镱轶镱禊溴骈铄轭翳稚抛轭翳视妹镱翦溴箝珙狒邃怡孟卧咆原呐由俏猎弦鏖翳翳瘟团狍箝珙邃麸轸瘟团轶彘翳弪簌礅镬篝蜷铉矧铋旌御礅镬趱蝾轭麸汜礤煦狍视骢钽糸镱钺礤郁蜷铉钺礤铄骢钽糸镱溟蝈泗禊铋玳鲥铒钺礤麸翳铄骢钽糸镱磲腴铉轸犷镱盹躞渝噻弭牦悱泔铘屮臾骘麒狒孟卧咆原呐由俏猎弦泔蹯忮ㄤ弩趄蹉趱蜷铉忾钿鲩鬻镳糸镱犰钺礤泔铘屮舡溴箝珙狒矧蹰镳哄铙躜瀛扉篝礤翎溽翎戾舄è牦钺礤ㄥ豉疱汜箦钺礤篝蜷铉钺礤簌礅镬ㄣ骀楹趄犷箪狒瀛汜礤煦狍瀛钺礤钺礤┅铛祆ㄣ骀楹铛祆痫轭翦颟┅ㄣ犰焘徙氕钺礤ㄩ铘弪ㄦ矧磲铋岘昧烫铝盟ㄧ孱簌牦钺礤┅┅瞽狎珞戾铉翳狎珞┅躞弪溽翎ㄧ孱簌愆┅啜痱镧ㄤ彐汜祆忉汶汜祆忉汶钺礤ㄧ镡赍泗牦悱鲠祯濠ì括祜镳骘狎轭狎珞泔祆邈啜狎吼镩铘弪┅ì躞弪溽翎吼镩铘弪┅ㄤ邈灬蝈ㄩ珙矧徕戾躞弪溽翎泪蜱螬戾ì括祜镳骘狎轭狎珞泔祆邈啜狎牦悱鲠祯瀛麸扉箴狎绌┅扉箴麸牦悱鲠祯痱镧棱镤ㄧ弭牦悱泔铘屮鲩鬻泔铘屮舡溴箝珙狒矧┅┅ē磲脲牦悱骢钽糸镱鲩鬻泔铘屮舡溴箝珙狒矧牦钺礤ㄣ骀楹汜祆忉汶汜祆忉汶钺礤瞽狎珞┅┅